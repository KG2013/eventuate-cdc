buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:$springBootPluginVersion")
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath "com.avast.gradle:gradle-docker-compose-plugin:0.12.0"
    }
}

plugins {
    id "com.github.hierynomus.license" version "0.12.1"
}

apply plugin: 'docker-compose'

allprojects {
    group = "io.eventuate.cdc"
}

projects {
    license {
        header rootProject.file('src/license/HEADER')
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'com.jfrog.bintray'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    bintray {
        publish = true
        user = System.getenv('BINTRAY_USER')
        key = System.getenv('BINTRAY_KEY')
        configurations = ['archives']
        pkg {
            repo = "eventuate-maven-$bintrayRepoType"
            name = 'eventuate-cdc'
            licenses = ['Apache-2.0']
            vcsUrl = 'https://github.com/eventuate-foundation/eventuate-cdc'
        }
    }

    repositories {
        mavenCentral()
        jcenter()
        eventuateMavenRepoUrl.split(',').each { repoUrl -> maven { url repoUrl } }
    }

    dependencies {
        testCompile "junit:junit:4.12"
    }

    configurations {
        deployerJars
    }

    dependencies {
        deployerJars 'org.springframework.build:aws-maven:5.0.0.RELEASE'
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                configuration = configurations.deployerJars
                repository(url: deployUrl) {
                    authentication(userName: System.getenv('S3_REPO_AWS_ACCESS_KEY'), password: System.getenv('S3_REPO_AWS_SECRET_ACCESS_KEY'))
                }
                pom.project {
                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }
                }
            }
        }
    }
}

if (!project.ext.has("database")) {
    project.ext.set("database", System.env.DATABASE == null || System.env.DATABASE == "" ? "mysql" : "$System.env.DATABASE")
}

if (!project.ext.has("mode")) {
    project.ext.set("mode", System.env.MODE == null || System.env.DATABASE == "" ? "binlog" : "$System.env.MODE")
}

if (mode == "unified") {
    project.ext.set("databaseDockerFile", "docker-compose-unified.yml")
    project.ext.set("databaseDockerCdcFile", "docker-compose-cdc-unified.yml")
} else {
    project.ext.set("databaseDockerFile", "docker-compose-${database}.yml")
    project.ext.set("databaseDockerCdcFile", "docker-compose-cdc-${database}-${mode}.yml")
}

def services = { ->
    if (!project.ext.has("composeServices")) {
        return System.env.COMPOSE_SERVICES == null || System.env.COMPOSE_SERVICES == "" ? [] : "$System.env.COMPOSE_SERVICES".split(",")
    } else {
        return "$composeServices".split(",")
    }
}

dockerCompose {
    environment.put "EVENTUATE_COMMON_VERSION", eventuateCommonImageVersion
    environment.put "EVENTUATE_ACTIVE_MQ_VERSION", eventuateActiveMQImageVersion
    environment.put "EVENTUATE_RABBIT_MQ_VERSION", eventuateRabbitMQImageVersion

    removeOrphans = true

    mysql {
        projectName = null
        useComposeFiles = ["docker-compose-mysql.yml"]
        removeContainers = project.ext.removeContainers
    }

    mysqlmigration {
        projectName = null
        removeContainers = project.ext.removeContainers
        useComposeFiles = ["docker-compose-eventuate-local-mysql-for-migration.yml"]
        startedServices = services()
    }

    mariadb {
        projectName = null
        useComposeFiles = ["docker-compose-mariadb.yml"]
        removeContainers = project.ext.removeContainers
    }

    mssql {
        projectName = null
        useComposeFiles = ["docker-compose-mssql.yml"]
        removeContainers = project.ext.removeContainers
    }

    postgres {
        projectName = null
        useComposeFiles = ["docker-compose-postgres.yml"]
        removeContainers = project.ext.removeContainers
    }

    mysqljson {
        projectName = null
        useComposeFiles = ["docker-compose-mysql-json.yml"]
        removeContainers = project.ext.removeContainers
    }

    mariadbjson {
        projectName = null
        useComposeFiles = ["docker-compose-mariadb-json.yml"]
        removeContainers = project.ext.removeContainers
    }

    mssqljson {
        projectName = null
        useComposeFiles = ["docker-compose-mssql-json.yml"]
        removeContainers = project.ext.removeContainers
    }

    postgresjson {
        projectName = null
        useComposeFiles = ["docker-compose-postgres-json.yml"]
        removeContainers = project.ext.removeContainers
    }

    unified {
        projectName = null
        useComposeFiles = ["docker-compose-unified.yml", "docker-compose-cdc-unified.yml"]
        removeContainers = project.ext.removeContainers
    }
}

task aggregatedTestReport(type: TestReport) {
  destinationDir = file("$buildDir/reports/allTests")
  // Include the results from the 'test' task in all subprojects
  reportOn subprojects*.test
}

gradle.projectsEvaluated {
    task aggregateJavaDocs(type: Javadoc) {
        description = 'Aggregates Javadoc API documentation of all subprojects.'
        group = JavaBasePlugin.DOCUMENTATION_GROUP
        dependsOn subprojects.findAll { subproject -> subproject.plugins.hasPlugin(PublicModulePlugin) }.javadoc

        source subprojects.findAll { subproject -> subproject.plugins.hasPlugin(PublicModulePlugin) }.javadoc.source
        destinationDir file("$buildDir/docs/javadoc")
        classpath = files(subprojects.findAll { subproject -> subproject.plugins.hasPlugin(PublicModulePlugin) }.javadoc.classpath)
    }
}
